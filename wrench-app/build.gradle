apply plugin: 'com.android.application'

android {

    dataBinding {
        enabled = true
    }

    compileSdkVersion 27
    defaultConfig {
        applicationId "com.izettle.wrench"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 9
        versionName "1.0.8"

        testInstrumentationRunner "com.izettle.wrench.WrenchTestRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        def configurationAuthorityValue = applicationId + ".configprovider"
        def wrenchPermission = applicationId + ".permission"

        manifestPlaceholders = [configurationAuthority: configurationAuthorityValue, wrenchPermission: wrenchPermission]

        buildConfigField "String", "CONFIG_AUTHORITY", "\"${configurationAuthorityValue}\""

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-stetho.pro'
        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-stetho.pro'

            versionNameSuffix " debug"
            applicationIdSuffix = ".debug"
        }
    }
    lintOptions {
        baseline file("lint-baseline.xml")
        checkReleaseBuilds true
        abortOnError true
        warningsAsErrors true
        lintConfig file("../lint.xml")
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2-alpha1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2-alpha1'
    androidTestImplementation 'android.arch.persistence.room:testing:1.1.0-alpha1'

    annotationProcessor "android.arch.lifecycle:compiler:1.1.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.0-alpha1"
    annotationProcessor "com.google.dagger:dagger-android-processor:2.14.1"
    annotationProcessor "com.google.dagger:dagger-compiler:2.14.1"

    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta4'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'android.arch.lifecycle:extensions:1.1.0'
    implementation 'android.arch.persistence.room:runtime:1.1.0-alpha1'
    implementation "android.arch.paging:runtime:1.0.0-alpha5"

    implementation "com.google.dagger:dagger:2.14.1"
    implementation "com.google.dagger:dagger-android:2.14.1"
    implementation "com.google.dagger:dagger-android-support:2.14.1"

    implementation project(":wrench-core")
    implementation project(":wrench-prefs")
}
